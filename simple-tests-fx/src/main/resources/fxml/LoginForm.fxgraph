package fxml

import javafx.scene.layout.BorderPane
import javafx.scene.layout.HBox
import javafx.scene.layout.VBox
import javafx.scene.layout.GridPane
import javafx.scene.control.Label
import javafx.scene.control.TextField
import javafx.scene.control.PasswordField
import javafx.scene.control.Separator
import javafx.geometry.Insets
import javafx.scene.layout.ColumnConstraints
import javafx.scene.control.Button

component LoginForm {
	BorderPane id content {
		padding : Insets {
			top : 5,
			right : 5,
			bottom : 5,
			left : 5
		},
		top : VBox {
			Label {
				text : "Enter you credentials"
			},
			Separator {
				static margin : Insets {
					top : 5,
					bottom : 5
				}
			}
		},
		center : GridPane {
			hgap : 5,
			vgap : 5,
			columnConstraints : [
				ColumnConstraints {
					minWidth : 100
				},
				ColumnConstraints {
					hgrow : "ALWAYS"
				}
			],
			Label {
				text : "Username",
				static columnIndex : 0,
				static rowIndex : 0
			},
			TextField id userText {
				promptText : "username",
				static columnIndex : 1,
				static rowIndex : 0
			},
			Label {
				text : "Password",
				static columnIndex : 0,
				static rowIndex : 1
			},
			PasswordField id passwdText {
				promptText : "passwd",
				static columnIndex : 1,
				static rowIndex : 1
			}
		},
		bottom : VBox id bottomBar {
			managed : bind bottomBar#visible,
			visible : false,
			Separator {
				static margin : Insets {
					top : 5,
					bottom : 5
				}
			},
			HBox {
				spacing : 5,
				alignment : "CENTER_RIGHT",
				Button id okButton {
					text : "Login",
					defaultButton : true
				},
				Button id cancelButton {
					text : "Cancel"
				}
			}
		}
	}
}